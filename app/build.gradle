apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.emarsys.mobileengage.sample"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.emarsys.mobileengage.sample.testutils.SampleAppTestRunner"
    }
    if (project.hasProperty('development') || System.env.DEVELOPMENT_MODE) {
        signingConfigs {
            release {
                storeFile file(System.env.RELEASE_STORE_FILE ?: releaseStoreFile)
                storePassword System.env.RELEASE_STORE_PASSWORD ?: releaseStorePassword
                keyAlias System.env.RELEASE_KEY_ALIAS ?: releaseKeyAlias
                keyPassword System.env.RELEASE_KEY_PASSWORD ?: releaseKeyPassword
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    maven {
        url "http://dl.bintray.com/emarsys-mobile/android"
    }
    if (project.hasProperty('development') || System.env.DEVELOPMENT_MODE) {
        mavenLocal()
    }
}

dependencies {
    if (project.hasProperty('development') || System.env.DEVELOPMENT_MODE) {
        implementation 'com.emarsys:mobile-engage-sdk:+'
    } else {
        implementation 'com.emarsys:mobile-engage-sdk:1.7.1'
    }
    // Getting a "Could not find firebase-core" error? Make sure you have
    // the latest Google Repository in the Android SDK manager

    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation 'com.squareup.picasso:picasso:2.5.2'


    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('com.android.support.test:runner:1.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('com.android.support.test:rules:1.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'